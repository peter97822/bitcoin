Fix python check

Upsream bugreports:
 - https://bugreports.qt.io/browse/QTBUG-72926
 - https://bugreports.qt.io/browse/QTBUG-84038

Upstream commits:
 - Qt 5.15: 1c6a70e1880a8fe0f3d8da4932c4f3533693219c
 - Qt 6.0:  2b41025fc967b37c8bff77c4b75bab333ae13f46

--- old/qtdeclarative/qtdeclarative.pro
+++ new/qtdeclarative/qtdeclarative.pro
@@ -1,10 +1,2 @@
 CONFIG += tests_need_tools examples_need_tools
 load(qt_parts)
-
-!python_available {
-    py_out = $$system('python -c "print(1)"')
-    !equals(py_out, 1): error("Building QtQml requires Python.")
-    tmp = python_available
-    CONFIG += $$tmp
-    cache(CONFIG, add, tmp)
-}

--- old/qtdeclarative/src/3rdparty/masm/masm.pri
+++ new/qtdeclarative/src/3rdparty/masm/masm.pri
@@ -58,7 +58,7 @@
     udis86.output = udis86_itab.h
     udis86.input = ITAB
     udis86.CONFIG += no_link
-    udis86.commands = python $$PWD/disassembler/udis86/itab.py ${QMAKE_FILE_IN}
+    udis86.commands = $QMAKE_PYTHON $$PWD/disassembler/udis86/itab.py ${QMAKE_FILE_IN}
     QMAKE_EXTRA_COMPILERS += udis86
 
     udis86_tab_cfile.target = $$OUT_PWD/udis86_itab.c
@@ -113,7 +113,7 @@
 retgen.script = $$PWD/yarr/create_regex_tables
 retgen.input = retgen.script
 retgen.CONFIG += no_link
-retgen.commands = python $$retgen.script > ${QMAKE_FILE_OUT}
+retgen.commands = $$QMAKE_PYTHON $$retgen.script > ${QMAKE_FILE_OUT}
 QMAKE_EXTRA_COMPILERS += retgen
 
 # Taken from WebKit/Tools/qmake/mkspecs/features/unix/default_post.prf

--- old/qtdeclarative/src/qml/configure.json
+++ new/qtdeclarative/src/qml/configure.json
@@ -23,6 +23,11 @@
                 ],
                 "qmake": "CONFIG += c++11"
             }
+        },
+        "qml-python": {
+            "label": "python",
+            "type": "detectPython",
+            "log": "location"
         }
     },
 
@@ -123,9 +128,25 @@
             "section": "QML",
             "condition": "features.thread",
             "output": [ "privateFeature" ]
+        },
+        "qml-python": {
+            "label": "python",
+            "condition": "tests.qml-python",
+            "output": [
+                "privateFeature",
+                { "type": "varAssign", "name": "QMAKE_PYTHON", "value": "tests.qml-python.location" }
+            ]
         }
     },
 
+    "report": [
+        {
+            "type": "error",
+            "condition": "!features.qml-python",
+            "message": "Python is required to build QtQml."
+        }
+    ],
+
     "summary": [
         {
             "section": "Qt QML",

--- old/qtdeclarative/src/qml/configure.pri
+++ new/qtdeclarative/src/qml/configure.pri
@@ -0,0 +1,22 @@
+equals(QMAKE_HOST.os, Windows): EXE_SUFFIX = .exe
+
+defineTest(qtConfTest_detectPython) {
+    PYTHON_NAMES = python$$EXE_SUFFIX python2$${EXE_SUFFIX} python3$${EXE_SUFFIX} py$${EXE_SUFFIX}
+    for (name, PYTHON_NAMES) {
+        python_path = $$qtConfFindInPath("$$name")
+        !isEmpty(python_path): \
+            break()
+    }
+    isEmpty(python_path) {
+        qtLog("No $$PYTHON_NAMES are found in PATH. Giving up.")
+        return(false)
+    }
+
+    # Make tests.python.location available in configure.json.
+    $${1}.location = $$clean_path($$python_path)
+    export($${1}.location)
+    $${1}.cache += location
+    export($${1}.cache)
+
+    return(true)
+}

--- old/qtdeclarative/src/qml/qml.pro
+++ new/qtdeclarative/src/qml/qml.pro
@@ -7,6 +7,9 @@
 TRACEPOINT_PROVIDER = $$PWD/qtqml.tracepoints
 CONFIG += qt_tracepoints
 
+!qtConfig(qml-python): \
+    error(Python is required to build QtQml.)
+
 DEFINES   += QT_NO_URL_CAST_FROM_STRING QT_NO_INTEGER_EVENT_COORDINATES
 
 msvc:equals(QT_ARCH, i386): QMAKE_LFLAGS += /BASE:0x66000000
@@ -81,8 +84,9 @@
 }
 include(types/types.pri)
 include(../3rdparty/masm/masm-defs.pri)
-include(../3rdparty/masm/masm.pri)
 
 MODULE_PLUGIN_TYPES = \
     qmltooling
 load(qt_module)
+
+include(../3rdparty/masm/masm.pri)
